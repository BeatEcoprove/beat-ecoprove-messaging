defmodule Messaging.Redis do
  use GenServer

  alias Redix

  def start_link(_opts) do
    opts = Application.fetch_env!(:messaging, __MODULE__)
    GenServer.start_link(__MODULE__, opts, name: __MODULE__)
  end

  def set(key_module, value) when is_atom(key_module) do
    key = key_module.key(value)
    GenServer.call(__MODULE__, {:set, key, value})
  end

  def get(key_module, identifier) when is_atom(key_module) do
    key = key_module.key(identifier)
    GenServer.call(__MODULE__, {:get, key})
  end

  def init(opts) do
    host = Keyword.get(opts, :host, "localhost")
    port = Keyword.get(opts, :port, "6379")
    db = Keyword.get(opts, :db, 0)

    {:ok, conn} =
      Redix.start_link(
        host: host,
        port: port,
        database: db
      )

    {:ok, %{conn: conn}}
  end

  def handle_call({:set, key, value}, _from, %{conn: conn} = state) do
    Redix.command(conn, ["SET", key, value])
    {:reply, :ok, state}
  end

  def handle_call({:get, key}, _from, %{conn: conn} = state) do
    {:ok, value} = Redix.command(conn, ["GET", key])
    {:reply, value, state}
  end
end
